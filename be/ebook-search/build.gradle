plugins {
    id 'java'
    id "org.sonarqube" version "4.4.1.3373"
    id 'jacoco'
}

allprojects {
    group = 'com.meetyourbook'
    version = '1.0-SNAPSHOT'
    
    repositories {
        mavenCentral()
    }
}

sonar {
    properties {
        property "sonar.projectKey", "MeetYourBook_meet-your-book"
        property "sonar.organization", "meetyourbook"
        property "sonar.host.url", "https://sonarcloud.io"
        property "sonar.coverage.jacoco.xmlReportPaths", "${buildDir}/reports/jacoco/test/jacocoTestReport.xml"
    }
}

subprojects {
    apply plugin: 'java'
    apply plugin: 'jacoco'

    java {
        toolchain {
            languageVersion = JavaLanguageVersion.of(21)
        }
    }

    jacoco {
        toolVersion = "0.8.12"
    }

    jacocoTestReport {
        reports {
            xml.required = true
            csv.required = false
            html.outputLocation = layout.buildDirectory.dir('jacocoHtml')
        }
    }

    test {
        useJUnitPlatform()
        finalizedBy jacocoTestReport
    }

    dependencies {
        implementation 'org.springframework.boot:spring-boot-starter-web'
        implementation 'org.springframework.boot:spring-boot-starter-validation'
        implementation 'org.springdoc:springdoc-openapi-starter-webmvc-ui:2.6.0'
        compileOnly 'org.projectlombok:lombok'
        annotationProcessor 'org.projectlombok:lombok'
        testImplementation 'org.springframework.boot:spring-boot-starter-test'
    }

    sonar {
        properties {
            property "sonar.sources", "src/main/java"
            property "sonar.tests", "src/test/java"
        }
    }

    tasks.named('sonar').configure {
        dependsOn jacocoTestReport
    }
}